{smcl}
{* 16Mar2020}{...}
{cmd:help smle}{right:also see:  {helpb smle postestimation}}
{hline}

{title:Title}

{p2colset 5 15 22 2}{...}
{p2col :{hi: smle} {hline 2}}Simulation-based estimation of peer effects{p_end}
{p2colreset}{...}

{title:Syntax}

{p 8 16 2}
{opt smle} {depvar} {it: {help varlist : controlvars}} {ifin} [{cmd:, } 
{opth peer:avg(varname)}
{opth np:eers(varname)}
{opth gr:oupid(varname)}
{it:options} ]

{synoptset 30 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:Model}
{synopt :{opth gr:oupid(varname)}}variable identifying the peer group (if group-based sample){p_end}
{synopt :{opth peer:avg(varname)}}variable describing peer average choice (if individual-based sample){p_end}
{synopt :{opth np:eers(varname)}}variable describing number of peers (if individual-based sample){p_end}

{synopt :{opth agg:gregate(varlist)}}additional aggregate control variables (will not be used to estimate rho_x){p_end}
{synopt :{opt rhot:ype}}rule for calculating cross-peer correlation in unobservables (rho_e){p_end}
{synopt :{opt rho(#)}}value for rho_epsilon (if rhotype == "Fixed") {p_end}
{synopt :{opt fixg:amma}}fix the value of peer effect (gamma) rather than estimating it{p_end}
{synopt :{opt gam:ma(#)}}value for gamma (if fixgamma is on){p_end}
{synopt :{opt eq:uilibrium(string)}}equilbrium selection rule{p_end}
{synopt :{opt und:erreporting}}include underreporting correction{p_end}
{synopt :{opt cov:mat(string)}}under construction{p_end}

{syntab:Simulation/optimization}
{synopt :{opt ns:im(#)}}number of simulations; default is 100.  {p_end}
{synopt :{opt rest:arts(#)}}number of restarts for DFP optimizer; default is 3{p_end}
{synopt :{opt sim:ulator(string)}}simulator; default is "GHK".  {p_end}
{synopt :{opt opt:imizer(string)}}optimization method; default is "DFP".  {p_end}
{synopt :{opt ufile(string)}}under construction{p_end}

{syntab:Execution/system}
{synopt :{opt noexe:cute}}do not estimate model{p_end}
{synopt :{opt save(string)}}save intermediate files{p_end}
{synopt :{opt replace}}overwrite existing files{p_end}
{synoptline}
{p 4 6 2}
Either {cmd:groupid} (for group-based samples) or 
{cmd:peeravg} and {cmd:npeers} (for individual-based samples)
must be provided.{p_end}
{p 4 6 2}
weights are not supported.{p_end}
{p 4 6 2}
Postestimation commands supported:{help estat summarize}.{p_end}

{title:Description}

{pstd}
{cmd:smle} can be used to estimate the Krauth (2006) model of discrete choice with social interactions.
This model features a binary choice ({it:depvar}) which responds to peer average choice
({it:peeravg}), individual-level control variables ({it:controlvars}), optional aggregate-level
control variables (specified with the {it:aggregate} option), and a probit unoabservable term.
{p_end}

{pstd}
{cmd:smle} serves as a wrapper for one of two external Fortran programs that do the actual estimation.
Depending on the data set and options, estimation can take anywhere from seconds to weeks.
The {opt noexecute} option allows you to generate the files needed to run the Fortran 
program and run it separately by hand.
{p_end}

{pstd}
{cmd:smle} estimates parameters but not standard errors. 
Use Stata's {help bootstrap} prefix to get standard errors, confidence intervals, etc.
{p_end}

{title:Options}

{dlgtab:Model}

{phang}
{opth groupid(varname)} identifies the group ID, if you have a group-based sample. If {cmd: groupid} is provided,
the program will use it to calculate {cmd:peeravg} and {cmd:npeers} from the data.  If {cmd: groupid} is {it:not} provided, 
both {cmd:peeravg} and {cmd:npeers} must be provided.
{p_end}

{phang}
{opth peeravg(varname)} provides the peer average choice, if you have an individual-based sample.
{p_end}

{phang}
{opth npeers(varname)} provides the number of peers used to construct {cmd:peeravg}, if you have an individual-based sample.
{p_end}

{phang}
{opth aggregate(varlist)} provides additional control variables to include in the model. Unlike the variables in {it:controlvars},
these variables will not be included in the calculation of rho_x.
{p_end}

{phang}
{opt rhotype(string)} allows alternative ways of estimating the within-peer-group correlation in unobservables (rho_e).
	Options include X, Fixed, Estimate, and Interval. Interval is not currently supported within Stata.
{p_end}

{phang}
{opt rho(#)} sets the fixed value for rho_e. It only applies if {cmd:rhotype("Fixed")}.
{p_end}

{phang}
{opt fixgamma} provides the option to fix the peer effect (gamma) at some predetermined value.
{p_end}

{phang}
{opt gamma(#)} sets the fixed value for gamma. It only applies if {cmd:fixgamma}.
{p_end}

{phang}
{opt equilibrium(string)} allows the user to set the equilibrium selection rule. Options include
	Low (the default), High, Random, Bounds, Plot and Minimum. Bounds, Plot and Minimum are not 
	currently supported within Stata.
{p_end}

{phang}
{opt underreporting} adds an adjustment for differential reporting of own choice versus peer choice.
	See Krauth (2006) for details.
{p_end}

{phang}
{opt covmat(string)} is under construction.
{p_end}


{dlgtab:Simulation/optimization}

{phang}
{opt nsim(#)} specifies the number of simulations used to approximate the likelihod function. 
The default is 100.
{p_end}

{phang}
{opt restarts(#)} specifies the number of restarts for the Davidson-Fletcher-Powell (DFP) optimizer. The default is 3.
{p_end}

{phang}
{opt simulator(string)} specifies the simulator to be used. Options are GHK (the default) and Hybrid.
{p_end}

{phang}
{opt optimizer(string)} specifies the otpimization method to be used. Options are DFP (the default) and SA.
{p_end}

{phang}
{opt ufile(string)} is under construction.
{p_end}

{dlgtab:Execution/system}

{phang}
{opt noexecute} tells {cmd:smle} not to run the Fortran program to generate estimates. Instead, 
the intermediate files are saved so that the user can run the Fortran program separately.
{p_end}

{phang}
{opt save(string)} identifies a folder in which to save intermediate files. If {cmd:save} is
not provided, intermediate files will be saved in the working directory (if {cmd:noexecute})
or in temporary files (otherwise).
{p_end}

{phang}
{opt replace} permits {cmd:smle} to overwrite existing files.
{p_end}

{title:Examples:}

{pstd}Setup{p_end}
{phang2}{cmd:. use http://www.sfu.ca/~bkrauth/code/smle_example.dta, clear}{p_end}

{pstd}An ordinary probit regression for comparison{p_end}
{phang2}{stata probit own peeravg pop}{p_end}

{pstd}Basic usage  - output parameter and data files for estimation only{p_end}
{phang2}{stata smle own peeravg npeers pop , noexecute replace }{p_end}

{pstd}Basic usage - estimate model{p_end}
{phang2}{stata smle own peeravg npeers pop , replace }{p_end}

{marker results}{...}
{title:Stored results}

{pstd}
{cmd:smle} stores the following in {cmd:e()}:

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Scalars}{p_end}
{synopt:{cmd:e(N)}}number of observations{p_end}
{synopt:{cmd:e(loglik)}}log-likelihood{p_end}
{synopt:{cmd:e(nsim)}}number of simulations{p_end}
{synopt:{cmd:e(restarts)}}number of restarts{p_end}
{synopt:{cmd:e(nvar)}}number of control variables{p_end}
{synopt:{cmd:e(numagg)}}number of control variables that are aggregate{p_end}
{synopt:{cmd:e(fixed_rho)}}value of rho if {cmd:rhotype("Fixed")}{p_end}
{synopt:{cmd:e(fixed_gamma)}}value of gamma if {cmd:fixgamma}{p_end}
{synopt:{cmd:e(report_rate)}}reporting rate if {cmd:underreporting} - not currently available{p_end}


{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Macros}{p_end}
{synopt:{cmd:e(cmd)}}{cmd:smle}{p_end}
{synopt:{cmd:e(cmdline)}}the full command line as called{p_end}
{synopt:{cmd:e(rhotype)}}rho type{p_end}
{synopt:{cmd:e(simulator)}}simulator{p_end}
{synopt:{cmd:e(title)}}{cmd:SMLE model}{p_end}
{synopt:{cmd:e(properties)}}{cmd:b V}{p_end}
{synopt:{cmd:e(depvar)}}name of dependent variable{p_end}

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Matrices}{p_end}
{synopt:{cmd:e(b)}}coefficient vector{p_end}
{synopt:{cmd:e(V)}}a square matrix of zeros. {cmd: smle} does not estimate a covariance matrix, but 
	some Stata features require one to exist.{p_end}

{synoptset 20 tabbed}{...}
{p2col 5 20 24 2: Functions}{p_end}
{synopt:{cmd:e(sample)}}marks estimation sample{p_end}
{p2colreset}{...}

{title:Also see}

{psee}
Krauth, Brian V. "Simulation-based estimation of peer effects."  {it: Journal of Econometrics}, 2006.  

{psee}
Online:  {help smle postestimation}
{p_end}
